apply plugin: 'com.android.library'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'

android {
    namespace 'com.ketch.android'
    compileSdkVersion buildConfig.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        minSdkVersion buildConfig.minSdk
        targetSdkVersion buildConfig.targetSdk
        versionCode buildConfig.versionCode
        versionName buildConfig.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "runnerBuilder", "de.mannodermaus.junit5.AndroidJUnit5Builder"

        consumerProguardFiles "consumer-rules.pro"
    }

    packagingOptions {
        exclude 'META-INF/*.kotlin_module'
        exclude 'META-INF/LICENSE-notice.md'
        pickFirst 'META-INF/AL2.0'
        pickFirst 'META-INF/LGPL2.1'
        pickFirst 'META-INF/LICENSE.md'
        pickFirst 'lib/*/libc++_shared.so'
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }

    kotlin {
        jvmToolchain(versions.javaVersion)
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    lint {
        abortOnError true
        baseline file('lint-baseline.xml')
        checkReleaseBuilds false
    }
}

dependencies {

    implementation "androidx.core:core-ktx:${versions.ktx}"
    implementation "androidx.appcompat:appcompat:${versions.appcompat}"
    implementation "com.google.android.material:material:${versions.material}"
    implementation "org.jetbrains.kotlin:kotlin-parcelize-runtime:${versions.parselize}"
    implementation "com.google.code.gson:gson:${versions.gson}"
    implementation "androidx.webkit:webkit:${versions.webkit}"

    testImplementation "junit:junit:${versions.junit}"
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}


afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.ketch.android'
                artifactId = 'ketchsdk'
                version = '3.0'
            }
        }
    }
}

dokkaHtmlPartial.configure {
    dokkaSourceSets {
        named("main") {
            // Used to remove a source set from documentation, test source sets are suppressed by default
            suppress.set(false)

            includeNonPublic.set(false)

            // Do not output deprecated members. Applies globally, can be overridden by packageOptions
            skipDeprecated.set(false)

            // Emit warnings about not documented members. Applies globally, also can be overridden by packageOptions
            reportUndocumented.set(true)

            // Do not create index pages for empty packages
            skipEmptyPackages.set(true)

            displayName.set("Ketch SDK")

            // Disable linking to online kotlin-stdlib documentation
            noStdlibLink.set(false)

            // Disable linking to online JDK documentation
            noJdkLink.set(false)

            // Disable linking to online Android documentation (only applicable for Android projects)
            noAndroidSdkLink.set(false)

            // Include generated files in documentation
            // By default Dokka will omit all files in folder named generated that is a child of buildDir
            suppressGeneratedFiles.set(false)
        }
    }
}
