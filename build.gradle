apply plugin: "com.github.ben-manes.versions"
apply plugin: "org.jlleitschuh.gradle.ktlint"
apply plugin: 'maven-publish'
apply plugin: "org.jetbrains.dokka"

buildscript {
    ext.buildConfig = [
            'minSdk'      : 26,
            'targetSdk'   : 36,
            'compileSdk'  : 36,
            'versionCode' : 3,
            'versionName' : '3.0'
    ]
    ext.versions = [
            'javaVersion' :  17,
            'kotlin'      : '2.1.20',
            'ktx'         : '1.17.0',
            'appcompat'   : '1.7.1',
            'dokka'       : '1.9.20',
            'junit'       : '4.13.2',
            'gson'        : '2.13.1',
            'webkit'      : '1.14.0'
    ]
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.kotlin:kotlin-serialization:${versions.kotlin}"
        classpath "de.mannodermaus.gradle.plugins:android-junit5:2.0.0"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.52.0"
        classpath "org.jlleitschuh.gradle:ktlint-gradle:14.0.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:${versions.dokka}"
    }
}

allprojects {
    repositories {
        google()
        mavenLocal()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url "https://dl.cloudsmith.io/public/indooratlas/mvn-public/maven" }
        maven { url "https://software.mobile.pendo.io/artifactory/android-release" }
    }
}

tasks.register('clean', Delete) {
    delete rootProject.layout.buildDirectory
}

ext {
    ketch_version = '0.3.' + System.getenv('GITHUB_RUN_NUMBER')
}

dokkaHtmlMultiModule.configure {
    // Set module name displayed in the final output
    moduleName.set("Ketch SDK")

    // Suppress obvious functions like default toString or equals. Defaults to true
    suppressObviousFunctions.set(false)

    // Suppress all inherited members that were not overridden in a given class.
    // Eg. using it you can suppress toString or equals functions but you can't suppress componentN or copy on data class. To do that use with suppressObviousFunctions
    // Defaults to false
    suppressInheritedMembers.set(true)
}

tasks.register('packageDoc', Zip) {
    archiveFileName = "sdk-doc.zip"
    destinationDirectory = layout.buildDirectory.dir("doc")
    from layout.buildDirectory.dir("dokka/htmlMultiModule/")
}
